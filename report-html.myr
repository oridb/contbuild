use std
use bio
use date
use iter

use "types"
use "repo"

pkg contbuild =
	const reporthtml	: (build : build#, targ : target#, report : report#, hash : byte[:] -> void)
;;

const reporthtml = {build, targ, report, hash
	/* only write this if we've configured html */
	if targ.htmldir.len == 0
		-> void
	;;

	match openreport(targ.htmldir, targ.name, "reports", targ.name, hash)
	| `std.Ok f:
		puthdr(f, targ.name, targ.htmlstyle)
		bio.put(f, "\t<div id=main>\n")
		putsidebar(f, build, targ._htmlbase)
		bio.put(f, "\t<article>\n")
		bio.put(f, "\t<h2>Build results: {}</h2>\n", targ.name)
		puthistory(f, targ)
		putstatus(f, targ, hash)
		reportcmd(f, &report.build, "build")
		reportcmd(f, &report.test, "test")
		reportcmd(f, &report.clean, "clean")
		bio.put(f, "\t</article>\n")
		bio.put(f, "\t</div>\n")
		putfooter(f)
		bio.close(f)
	| `std.Err e:
		std.put("unable to open directory: {}\n", e)
	;;

	match openreport(targ.htmldir, targ.name, "reports", targ.name, "summary")
	| `std.Ok f:
		puthdr(f, targ.name, targ.htmlstyle)
		bio.put(f, "\t<div id=main>\n")
		putsidebar(f, build, targ._htmlbase)
		bio.put(f, "\t<article>\n")
		bio.put(f, "\t<h2>Build Log</h2>\n")
		putsummary(f, targ)
		bio.put(f, "\t</article>\n")
		bio.put(f, "\t</div>\n")
		putfooter(f)
		bio.close(f)
	| `std.Err e:
		std.put("unable to open directory: {}\n", e)
	;;

	updateindex(build, targ)
}

const reportcmd = {f, c, phase
	bio.put(f, "<h3>{}: {}: {}</h3>\n", status(c.ok), phase, c.cmd)
	match c.ok
	| `Norun: -> void
	| _:	/* continue */
	;;
	bio.put(f, "<details>\n")
	bio.put(f, "<summary>STDOUT</summary>\n")
	bio.put(f, "<p><code><pre>\n")
	bio.put(f, "{}\n\n", c.out)
	bio.put(f, "</code></p>\n")
	bio.put(f, "</details>\n")

	bio.put(f, "<details>\n")
	bio.put(f, "<summary>STDERR</summary>\n")
	bio.put(f, "<p><code><pre>\n")
	bio.put(f, "{}\n\n", c.err)
	bio.put(f, "</code></p>\n")
	bio.put(f, "</details>\n")
}

const updateindex = {build, targ
	match openreport(targ.htmldir, "", "", "", "index")
	| `std.Ok f:
		puthdr(f, "All Builds", build.htmlstyle)
		bio.put(f, "\t<div id=main>\n")
		putsidebar(f, build, targ._htmlbase)
		bio.put(f, "\t<article>\n")
		for t : build.targs
			if !std.sleq(targ.htmldir, t.htmldir)
				continue
			;;
			bio.put(f, "\t\t<h2><a href={}/reports/{}/summary.html>{}: {}</a></h2>\n", \
				t._htmlbase, t.name, t.name, status(t._status))
			puthistory(f, t)
		;;
		bio.put(f, "\t</article>\n")
		bio.put(f, "\t</div>\n")
		putfooter(f)
		bio.close(f)
	| `std.Err e:
	;;
}

const putsummary = {f, targ
	var s
	bio.put(f, "<table>\n")
	for (hash, st) : iter.byreverse(tailents(targ._hist, 20))
		bio.put(f, "\t<tr>\n")
		if st
			bio.put(f, "<td style=\"width:1em;height:1em;border:1px solid white;\" bgcolor=green>")
		else
			bio.put(f, "<td style=\"width:1em;height:1em;border:1px solid white;\" bgcolor=red>")
			bio.put(f, "<a href={}/reports/{}/{}.html> </a>", \
				targ._htmlbase, targ.name, hash)
		;;
		bio.put(f, "<td><a href={}/reports/{}/{}.html>{}</a></td>", \
			targ._htmlbase, targ.name, hash, hash)

		s = summary(targ, hash)
		bio.put(f, "<td><a href={}/reports/{}/{}.html>{}</a></td>", \
			targ._htmlbase, targ.name, hash, s)
		std.slfree(s)
		bio.put(f, "\t</tr>\n")
	;;
	bio.put(f, "</table>\n")
}

const puthdr = {f, targname, style
	bio.put(f, "<html>\n")
	bio.put(f, "\t<head>\n")
	bio.put(f, "\t\t<meta http-eqiv=\"Content-Type\" content=\"text/html; charset=utf-8\" />\n")
	bio.put(f, "\t\t<link rel=\"stylesheet\" href=\"{}\" />\n", style)
	bio.put(f, "\t\t<title>Build Results: {}</title>\n", targname)
	bio.put(f, "\t\t<meta name=viewport content=\"width=device-width, initial-scale=1\" />\n")
	bio.put(f, "\t</head>\n")
	bio.put(f, "\t<body>\n")
	bio.put(f, "\t\t<header>\n")
	bio.put(f, "\t\t\t<h1>Build results: {}</h1>\n", targname)
	bio.put(f, "\t\t</header>\n")
}

const putstatus = {f, targ, hash
	var s

	s = commitmsg(targ, hash)
	bio.put(f, "\t\t<div id=\"status\">\n")
	if targ.commiturl.len == 0
		bio.put(f, "\t\t<p>[{}] {}</p>\n", status(targ._status), hash)
	else
		bio.put(f, "\t\t<p>[{}] <a href={}{}>{}</a>",
		    status(targ._status), targ.commiturl, hash, hash)
	;;
	bio.put(f, "\t\t<p><pre>{}</pre></p>\n", s)
	bio.put(f, "\t\t</div>\n")
	std.slfree(s)
}

const puthistory = {f, targ
	const lnkstyle="\"display:block;width:100%;height:100%;\""
	bio.put(f, "<table style=\"border:1px solid black;\"><tr>\n")
	for (hash, st) : tailents(targ._hist, 20)
		if st
			bio.put(f, "<td style=\"width:1em;height:1em;border:1px solid white;\" bgcolor=green>")
			bio.put(f, "<a style={} href={}/reports/{}/{}.html> </a>", \
				lnkstyle, targ._htmlbase, targ.name, hash)
			bio.put(f, "</td>\n")
		else
			bio.put(f, "<td style=\"width:1em;height:1em;border:1px solid white;\" bgcolor=red>")
			bio.put(f, "<a style={} href={}/reports/{}/{}.html> </a>", \
				lnkstyle, targ._htmlbase, targ.name, hash)
			bio.put(f, "</td>\n")
		;;
	;;
	bio.put(f, "</tr></table>\n")
}

const putsidebar = {f, build, htmlbase
	bio.put(f, "<nav><ul>")
	bio.put(f, "\t<li>\n")
	bio.put(f, "\t\t<a href={}/index.html>All Builds</a>\n", htmlbase)
	bio.put(f, "\t</li>\n")
	for t : build.targs
		bio.put(f, "\t<li>\n")
		bio.put(f, "\t\t<a href={}/reports/{}/summary.html>{}</a>\n", htmlbase, t.name, t.name)
		bio.put(f, "\t</li>\n")
	;;
	bio.put(f, "</ul></nav>")
}

const putfooter = {f
	bio.put(f, "\t<footer><p>contbuild</p></footer>\n")
	bio.put(f, "\t</body>\n")
	bio.put(f, "</html>\n")
}


const openreport = {htmldir, build, base, rdir, name
	var reportdir, reportpath, reportname
	var r

	reportname = std.strcat(name, ".html")
	reportdir = std.pathjoin([htmldir, base, rdir][:])
	reportpath = std.pathcat(reportdir, reportname)

	std.mkpath(htmldir)
	std.mkpath(reportdir)
	std.put("Writing {}\n", reportpath)
	r = bio.create(reportpath, bio.Wr, 0o644)

	std.slfree(reportname)
	std.slfree(reportpath)
	std.slfree(reportdir)

	-> r
}

const status = {s
	match s
	| `Norun:	-> "NO RUN"
	| `Succ:	-> "OK"
	| `Fail:	-> "FAIL"
	;;
}

generic tailents = {a : @a[:], n
	if a.len < n
		-> a
	else
		-> a[a.len - n : ]
	;;
}
