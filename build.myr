use std
use sys
use bio

use "types"
use "repo"
use "report-html"
use "report-email"

pkg contbuild =
	const run : (build : build# -> void)
;;

const run = {build
	for t : build.targs
		std.put("cloning {}...", t.repo)
		clone(t)
		std.put("done\n")
	;;

	while true
		for t : build.targs
			sync(t)
			std.chdir(t._repo)
			runonce(build, t)

		;;
		std.put("run done, delaying {}\n", build.period/std.Sec)
		std.usleep(build.period)
	;;
}

const runonce = {build, targ
	var r : report
	var ok, brlist

	r = [
		.build=[.cmd="", .out="", .err="", .ok=`Norun],
		.test=[.cmd="", .out="", .err="", .ok=`Norun],
		.clean=[.cmd="", .out="", .err="", .ok=`Norun],
	]

	brlist = std.tryv(branches(targ), [][:])
	for (cm, br) : brlist
		for u : updates(targ, br, cm)
			reset(&r)
			if !checkout(targ, u)
				continue
			;;
			purge(targ)
			ok = testrun(targ, &r)
			report(build, targ, &r, ok, u, br)
		;;
	;;
}

const testrun = {t, r
	-> runcmd(t, t.build, &r.build) && \
		runcmd(t, t.test, &r.test) && \
		runcmd(t, t.clean, &r.clean)
}

const updates = {targ, branch, commit
	match std.htget(targ._built, branch)
	| `std.None: 
		std.htput(targ._built, std.sldup(branch), std.sldup(commit))
		-> std.sldup([std.sldup(commit)][:])
	| `std.Some old:
		match between(targ, old, commit)
		| `std.Ok r:
			std.slfree(old)
			std.htput(targ._built, branch, std.sldup(commit))
			-> reverse(r)
		| `std.Err err:
			std.put("{}: unable to get updates for {}: {}\n",
			    targ.name, branch, err)
			std.slfree(err)
			-> [][:]
		;;
	;;
}

const reverse = {lst
	for var i = 0; i < lst.len / 2; i++
		std.swap(&lst[i], &lst[lst.len - i - 1])
	;;
	-> lst
}

const report = {build, targ, report, status, hash, branch
	var changed

	changed = isok(targ._status) != status

	/* update the book keeping */
	match status
	| true:	targ._status = `Succ
	| _:	targ._status = `Fail
	;;
	std.fput(build.logfd, "{} {} {} {}\n", hash, status, targ.name, branch)
	std.slpush(&targ._hist, (std.sldup(hash), status))

	reporthtml(build, targ, report, hash);

	/* annoying reports should only get fired if we change */
	if changed
		reportemail(build, targ, report, hash)
	;;
}

const reset = {r
	resetcmd(&r.build)
	resetcmd(&r.test)
	resetcmd(&r.clean)
}

const resetcmd = {c
	set(&c.out, "")
	set(&c.err, "")
	c.ok = `Norun
}

const isok = {s
	match s
	| `Succ:	-> true
	| _:	-> false
	;;
}

const set = {s, v
	std.slfree(s#)
	s# = v
}

const runcmd = {t, cmd, r
	set(&r.cmd, std.strjoin(cmd, " "))
	std.put("run {}\n", r.cmd)
	if cmd.len == 0
		-> true
	;;
	match readcmd(t, cmd)
	| `std.Ok (out, err):
		set(&r.out, out)
		set(&r.err, err)
		r.ok = `Succ
	| `std.Err (out, err):
		set(&r.out, out)
		set(&r.err, err)
		r.ok = `Fail
	;;
	-> isok(r.ok)
}
